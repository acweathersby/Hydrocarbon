# Copyright (C) 2021 Anthony Weathersby - The Hydrocarbon Parser Compiler
# see /source/typescript/hydrocarbon.ts for full copyright and warranty 
# disclaimer notice.

# version 5.0

@IMPORT ./symbols.hcg as sym

@IMPORT ./comments.hcg as cm

@IGNORE g:sp g:nl tk:cm::comment

########################################################################################################################
######################################  FUNCTIONS
########################################################################################################################

<> hcg_functions > referenced_function
    | error_function
    | reduce_function
    | function_clause

<> referenced_function > js_function_start_symbol sym::reference_symbol? { js_data }          
                                                                    f:r {  { 
                                                                            type:"ref-function",  
                                                                            id:$2,     
                                                                            txt:$4,
                                                                            tok
                                                                        }  }

<> out_of_band_function > js_function_start_symbol \^ sym::identifier { js_data }          
                                                                    f:r {  { 
                                                                            type:"out_of_band",  
                                                                            id:$3,     
                                                                            txt:$5,
                                                                            tok
                                                                        }  }

<> error_function > js_function_start_symbol t:erh { js_data } { js_data }                    f:r { 
                                                                       { 
                                                                           type:"ERROR_RECOVERY",     
                                                                        
                                                                            lexer_text:$4, 
                                                                            body_text:$7,
                                                                            tok
                                                                        }
                                                                    }
<> reduce_function > js_function_start_symbol ( t:return | t:r ) { js_data }                                 
                                                                    f:r { {
                                                                        type:"RETURNED",        
                                                                        txt:$4, 
                                                                        name:"", 
                                                                        env:false,
                                                                        ref:"",     
                                                                        IS_CONDITION:true,
                                                                            tok
                                                                        }
                                                                    }

     | js_function_start_symbol ( t:return | t:r ) \^  sym::identifier     
                                                                     f:r { {
                                                                            type:"env-function-reference",             
                                                                            ref:$4,
                                                                            tok
                                                                        }
                                                                    }
    | js_function_start_symbol ( t:return | t:r ) \=>  sym::identifier    
                                                                    f:r { {
                                                                            type:"local-function-reference",             
                                                                            ref:$4,
                                                                            tok
                                                                        } 
                                                                    }

<> function_clause > 
    
    (js_function_start_symbol f:r { $1+"DD" }) { js_data }                                 
                                                                    f:r { { 
                                                                            type:"function-inline",     
                                                                            txt:$3, 
                                                                            name:"",     
                                                                            env:false,
                                                                            IS_CONDITION:true,
                                                                            tok
                                                                        }
                                                                    }

    | (js_function_start_symbol f:r { $1+"DD" }) \^ sym::identifier                             
                                                                    f:r {
                                                                        {type:"INLINE",     
                                                                        txt:"", 
                                                                        name:$3,     
                                                                        env:true,
                                                                        IS_CONDITION:true,
                                                                        tok
                                                                        }
                                                                    }

<> js_data > js_primitive
    | js_data_block
    | js_data js_primitive                                            f:r { $1 + $2 }
    | js_data js_data_block                                            f:r { $1 + $2 }
    | $eof

<> js_primitive > g:id 
    | g:num 
    | g:sp 
    | g:sym 

<> js_data_block > \{ js_data \}                                    f:r { $1 + $2 + $3 }

<> js_function_start_symbol > \f:                                  
    
