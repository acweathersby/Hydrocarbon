# Copyright (C) 2021 Anthony Weathersby - The Hydrocarbon Parser Compiler
# see /source/typescript/hydrocarbon.ts for full copyright and warranty 
# disclaimer notice.

# version 5.0

@IMPORT ./comments.hcg as cm

@IMPORT ./production_bodies.hcg as pb

@IGNORE g:sp g:nl tk:cm::comment

#######################################################################################################################
######################################  SYMBOLS
########################################################################################################################

<> hcg_symbols > symbol(+)

<> condition_symbol_list > terminal_symbol(+)

<> lexer_symbols > lexer_symbol(+)

<> ignore_symbols > ignore_symbol(+)

<> lexer_symbol > 

        terminal_symbol

<> ignore_symbol > 
        
        terminal_symbol

<> annotated_symbol > symbol    

    |   symbol reference_symbol
        
        f:r { ($1.annotation = $2, $1) }

<> symbol > 

        symbol \?      
        
        f:r { $1.IS_OPTIONAL = 1, $1 }
    
    |   \<= symbol           
        
        f:r { { type:"look-behind", val:$2.val, phased: $2, tok } }

    |   \?= symbol
        
        f:r { $2.IS_NON_CAPTURE = true, $2 }

    |   terminal_symbol

    |   group_symbol

    |   production_symbol

    |   symbol ( \(+ | \(* )  terminal_symbol?  \)
        
        f:r { { type:"list-production", terminal_symbol:$$3, IS_OPTIONAL:+($2 == "(*"), val:$1, tok, meta:false, annotation:null } }

<> group_symbol > \( pb::production_bodies \)        
          
        f:r { { type:"group-production", val:$2, tok, meta:false, annotation:null } }  

<> production_token_symbol > 
    
    \tk: production_symbol       
    
        f:r { { type:"production_token", name:$2.name, production:$2, val:-1, tok, meta:false, annotation:null } }

<> production_symbol >

        local_production_symbol
    
    |   imported_production_symbol

<>  terminal_symbol > 

        generated_symbol

    |   literal_symbol

    |   escaped_symbol

    |   EOF_symbol

    |   production_token_symbol     

<> reference_symbol > tk:reference_symbol_token

        f:r { { type:"reference", val:tok.slice(1), tok } }

<> reference_symbol_token > \^ identifier

<> EOF_symbol > 
    
    \$eof
    
        f:r { { type:"eof", val:"END_OF_FILE", tok, meta:false, annotation:null } }

<> empty_symbol > 
    
    \$empty
    
        f:r { { type:"empty", val:"", tok, meta:false, annotation:null } }

<> generated_symbol > 
    
    \g: tk:identifier_syms
    
        f:r { { type:"generated", val:$2.toString(), tok, meta:false, annotation:null } }

<> literal_symbol > 
    
        \t: ( g:id | g:num )(+\" ) sym_delimiter? 
          
        f:r { { type:"exclusive-literal", val:$2.toString(), tok, meta:false, annotation:null } }
    
    |   ( g:sym | g:num )(+\" )

        f:r { { type:"literal", val:$1.toString(), tok, meta:false, annotation:null } }


<> escaped_symbol > 
    
    \\ ( g:id | g:sym | g:num )(+\" ) sym_delimiter           
    
        f:r { { type:"literal", val:$2.toString(), tok, meta:false, annotation:null } }

<> local_production_symbol > 
    
    tk:identifier_syms
        
        f:r { { type:"sym-production", name:$1.toString(), production:null, val:-1, tok, meta:false, annotation:null } }

<> imported_production_symbol > 

    tk:identifier_syms \:: tk:identifier_syms
        
        f:r { { type:"sym-production-import", module:$1.toString(), name:$3.toString(),  production:null, tok, meta:false, annotation:null } } 

<> annotation_identifier > ( g:sym | g:id | g:num )(+\" ) g:sp? f:r { $1 }

<> production_id > tk:identifier_syms

<> identifier > tk:identifier_syms 

<> identifier_syms >  

        identifier_syms (RST g:sp g:nl) g:id
        
        f:r { $1 + $2 }

    |   identifier_syms (RST g:sp g:nl) \_
        
        f:r { $1 + $2 }

    |   identifier_syms (RST g:sp g:nl) g:num      
        
        f:r { $1 + $2 }

    |   \_ 

    |   g:id

<> sym_delimiter >  g:sp | g:nl | ?=$eof

<> meta_symbol >   
        
        \(EXC condition_symbol_list \)

        f:r { { type:"meta-exclude", sym: $2, meta:true, index:-1, tok, annotation:null} }

    |   \(IGN condition_symbol_list \)    

        f:r { { type:"meta-ignore",  sym: $2, meta:true, index:-1, tok, annotation:null} }

    |   \(RST condition_symbol_list \)

        f:r { { type:"meta-reset",   sym: $2, meta:true, index:-1, tok, annotation:null} }

